
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Документ = Параметры.Документ;
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокТиповПолей = Новый Массив;
	СписокТиповПолей.Добавить("СтандартныеРеквизиты");
	СписокТиповПолей.Добавить("Измерения");
	СписокТиповПолей.Добавить("Ресурсы");
	СписокТиповПолей.Добавить("Реквизиты");
		
	Мета = Документ.Метаданные();
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого Движение Из Мета.Движения Цикл
		ТипРегистра = Лев(Движение.ПолноеИмя(),Найти(Движение.ПолноеИмя(),".") - 1);
		мРекв       = Новый РеквизитФормы(Движение.Имя,Новый ОписаниеТипов(СтрЗаменить(Движение.ПолноеИмя(),ТипРегистра,ТипРегистра + "НаборЗаписей")),,Движение.Синоним);
		ДобавляемыеРеквизиты.Добавить(мРекв);
	КонецЦикла;	
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для Каждого Движение Из Мета.Движения Цикл
		мНабор = РеквизитФормыВЗначение(Движение.Имя);
        мНабор.Отбор.Регистратор.Установить(Документ);
		мНабор.Прочитать();
		ЗначениеВРеквизитФормы(мНабор,Движение.Имя);
		
		Группа                 = Элементы.Добавить("Страница_" + Движение.Имя,Тип("ГруппаФормы"),Элементы.ГруппаСтраницы);
		Группа.Заголовок       = Движение.Синоним;
		Таблица                = Элементы.Добавить(Движение.Имя,Тип("ТаблицаФормы"),Группа);
		Таблица.ПутьКДанным    = Движение.Имя;
		Таблица.ТолькоПросмотр = Истина;
		Для Каждого ТипПоля Из СписокТиповПолей Цикл
			Для Каждого РеквМета Из Движение[ТипПоля] Цикл
				Поле = Элементы.Добавить(Движение.Имя + "_" + РеквМета.Имя,Тип("ПолеФормы"),Таблица);
				Поле.ПутьКДанным = Движение.Имя + "." + РеквМета.Имя;
				Если ТипПоля = "СтандартныеРеквизиты" Тогда
					Если РеквМета.Имя = "ВидДвижения" Тогда
						Поле.Ширина    = 5;
					Иначе
						Поле.Видимость = Ложь;
					КонецЕсли;	
				Иначе
					Поле.Ширина = 8;
				КонецЕсли;	
			КонецЦикла;	
		КонецЦикла;	
	КонецЦикла;	
КонецПроцедуры

