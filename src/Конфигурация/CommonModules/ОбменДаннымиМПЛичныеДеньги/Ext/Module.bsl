
/// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ ///

// Процедура вносит в информационную базу данные, которые присланы из узла "УзелОбмена" 
//
// Параметры:
//  УзелОбмена	– узел плана обмена "мобильные", с которым осуществляется обмен
//  ДанныеОбмена - пакет обмена полученный из узла УзелОбмена, помещен в ХранилищеЗначения
//
Процедура ПринятьПакетОбмена(УзелОбмена, ДанныеОбмена) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ДанныеОбмена.Получить());
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель,ЧтениеСообщения.НомерПринятого);
	
	НачатьТранзакцию();
	Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		Данные = ПрочитатьXML(ЧтениеXML);
		
		Если Не Данные = Неопределено Тогда
			
			Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
			Данные.ОбменДанными.Загрузка = Истина;
			
			Данные.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	
КонецПроцедуры

// Процедура регистрирует изменения, для всех данных, входящих в состав плана обмена
// Параметры:
//  УзелОбмена - узел плана обмена, для которого регистрируются изменения
Процедура ЗарегистрироватьИзмененияДанных(УзелОбмена) Экспорт
	//Пока обмен идет в одностороннем режиме с МП на локальный компьютер - регистрировать изменения данных не будем.
	Возврат;
	
	СоставПланаОбмена = УзелОбмена.Метаданные().Состав;
	Для Каждого ЭлементСоставаПланаОбмена Из СоставПланаОбмена Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена,ЭлементСоставаПланаОбмена.Метаданные);
	КонецЦикла;
	
КонецПроцедуры

// Считывает все изменения из информационной базы и возвращает их в виде хранилища значения //
Функция ПолучитьДанныеОбмена (УзелОбмена) Экспорт
	ИмяВФ = ПолучитьИмяВременногоФайла("xml");
	ЗаписьXML       = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВФ);	
	ЗаписьИзменений = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьИзменений.НачатьЗапись(ЗаписьXML,УзелОбмена);
 	ПланыОбмена.ЗаписатьИзменения(ЗаписьИзменений);
	ЗаписьИзменений.ЗакончитьЗапись();
	ЗаписьXML.Закрыть();
	
 	Чтение = Новый ЧтениеТекста(ИмяВФ);
	Чтение.Открыть(ИмяВФ);
	Текст  = Чтение.Прочитать();	
	Чтение.Закрыть();
	
	УдалитьФайлы(ИмяВФ);
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("НомерОтправленного", УзелОбмена.НомерОтправленного);
	СтруктураОтвета.Вставить("Текст",Текст);
	
	Возврат Новый ХранилищеЗначения(СтруктураОтвета, Новый СжатиеДанных(9));
КонецФункции	
