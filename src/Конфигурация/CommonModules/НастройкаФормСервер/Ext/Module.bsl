Процедура НастройкаПриСозданииФормы (Форма,Объект,ИмяТЧ) Экспорт
	ДобавляемыеРеквизиты = Новый Массив;
	Реквизит = Новый РеквизитФормы("УИД",Новый ОписаниеТипов("Строка"),"Объект." + ИмяТЧ);
	ДобавляемыеРеквизиты.Добавить(Реквизит);
	Реквизит = Новый РеквизитФормы("ПоляСозданы",Новый ОписаниеТипов("Булево"),"Объект." + ИмяТЧ);
	ДобавляемыеРеквизиты.Добавить(Реквизит);
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Команда = Форма.Команды.Добавить("ДобавитьСтроку");
	Команда.Действие    = "ДобавитьСтроку";
	Команда.Отображение = ОтображениеКнопки.Картинка;
	Команда.Картинка    = БиблиотекаКартинок.Добавление;
	Команда.Заголовок   = "";
	
	Если Объект[ИмяТЧ].Количество() = 0 Тогда
		НоваяСтрока = Объект[ИмяТЧ].Добавить();
	КонецЕсли;	
	
	// Создадим идентификаторы для строк Табличной части
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		СтрокаТЧ.УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор),"-","_");
	КонецЦикла;	
	
	СоздатьОбновитьЭлементыФормы(Форма,Объект,ИмяТЧ);
КонецПроцедуры	

Процедура СоздатьОбновитьЭлементыФормы (Форма,Объект, ИмяТЧ) Экспорт
	Элементы = Форма.Элементы;
	Команды  = Форма.Команды;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	СписокРеквизитовТЧ = ПолучитьСписокРеквизитовТЧ(Форма.ИмяФормы);
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		ОписаниеПолей = "";
	
		Если СтрокаТЧ.ПоляСозданы Тогда
			Продолжить;
		КонецЕсли;
		УИД = СтрокаТЧ.УИД;
		Для Каждого Реквизит Из СписокРеквизитовТЧ Цикл
			ИмяРекв = Реквизит.Ключ + "_" + УИД;
			мРекв   = Новый РеквизитФормы(ИмяРекв,Реквизит.Значение.ТипЗначения,,НСтр(Реквизит.Значение.Представление));
			ДобавляемыеРеквизиты.Добавить(мРекв);
			ОписаниеПолей = ОписаниеПолей + ?(ПустаяСтрока(ОписаниеПолей),"",", ") + НСтр(Реквизит.Значение.Представление);
		КонецЦикла;
		Команда = Команды.Добавить("Удалить_" + УИД);
		Команда.Действие    = "УдалитьСтроку";
		Команда.Отображение = ОтображениеКнопки.Картинка;
		Команда.Картинка    = БиблиотекаКартинок.Удаление;
		Команда.Заголовок   = "";
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		Если СтрокаТЧ.ПоляСозданы Тогда
			Продолжить;
		КонецЕсли;
		
		УИД = СтрокаТЧ.УИД;
		
		мГруппа = Элементы.Добавить("Группа_" + УИД,Тип("ГруппаФормы"),Элементы.ГруппаСтроки);
		мГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		мГруппа.Заголовок           = ОписаниеПолей;
		мГруппа.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаКнопки;
		мГруппа.ШрифтЗаголовка      = ШрифтыСтиля.КрупныйШрифтТекста;
		мГруппа.Отображение         = ОтображениеОбычнойГруппы.Нет;
		мГруппа.ОтображатьЗаголовок = ЛОЖЬ;
		мГруппа.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		мГруппа.Ширина              = 32;
		
		Если СтрокаТЧ.НомерСтроки = 1 Тогда
			Кнопка = Элементы.Добавить("Добавить_" + ИмяРекв,Тип("КнопкаФормы"),мГруппа);
			Кнопка.ИмяКоманды = "ДобавитьСтроку"; 
		Иначе	
			мГруппа.Отображение         = ОтображениеОбычнойГруппы.Нет;
			мГруппа.ОтображатьЗаголовок = Ложь;
			Кнопка = Элементы.Добавить("Удалить_" + ИмяРекв,Тип("КнопкаФормы"),мГруппа);
			Кнопка.ИмяКоманды = "Удалить_" + УИД; 
		КонецЕсли;	
		
		Кнопка.Ширина = 3;
		
		Для Каждого Реквизит Из СписокРеквизитовТЧ Цикл
			ИмяРекв          = Реквизит.Ключ + "_" + УИД;
			
			Форма[ИмяРекв] = СтрокаТЧ[Реквизит.Ключ];
			
			Поле             = Элементы.Добавить(ИмяРекв,Тип("ПолеФормы"),мГруппа);
			Поле.ПутьКДанным = ИмяРекв;
			Поле.Вид         = ВидПоляФормы.ПолеВвода;
			Поле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Поле.Шрифт              = ШрифтыСтиля.КрупныйШрифтТекста;
			Поле.КнопкаОткрытия = Ложь;
			Поле.УстановитьДействие("ПриИзменении","ПриИзмененииРеквизита");
			
			Если Реквизит.Значение.ТипЗначения.СодержитТип(Тип("Число")) Тогда
				Поле.КнопкаВыбора = Ложь;
				Поле.Ширина = 10;
			Иначе
				Поле.Ширина         = 20;
				Поле.ПодсказкаВвода = НСтр(Реквизит.Значение.Представление);
			КонецЕсли;
		КонецЦикла;
		
		СтрокаТЧ.ПоляСозданы = Истина;
	КонецЦикла;
КонецПроцедуры	

Процедура УдалитьЭлементыСтроки (Форма, Объект, ИмяТЧ, УИД)  Экспорт
	Элементы = Форма.Элементы;
	Команды  = Форма.Команды;
	
	ИскомыеСтроки = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("УИД",УИД)); 
	Для Каждого ИскомаяСтрока Из ИскомыеСтроки Цикл
		Объект[ИмяТЧ].Удалить(ИскомаяСтрока);
	КонецЦикла;	
	
	СписокРеквизитовТЧ = ПолучитьСписокРеквизитовТЧ(Форма.ИмяФормы);
	
	// Удалим элементы
	Элементы.Удалить(Элементы["Группа_" + УИД]);
	
	УдаляемыеРеквизиты = Новый Массив;
	
	Для Каждого Реквизит Из СписокРеквизитовТЧ Цикл
		УдаляемыеРеквизиты.Добавить(Реквизит.Ключ + "_" + УИД);	
	КонецЦикла;	
	
	Форма.ИзменитьРеквизиты(,УдаляемыеРеквизиты);
	
	Если Объект[ИмяТЧ].Количество() = 0 Тогда
		НоваяСтрока = Объект[ИмяТЧ].Добавить();
		НоваяСтрока.УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор),"-","_");
		СоздатьОбновитьЭлементыФормы(Форма,Объект,ИмяТЧ);
	КонецЕсли;	
КонецПроцедуры	

Функция ПолучитьСписокРеквизитовТЧ (ИмяФормы)
	мТипЧисло = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2));
	СписокРеквизитовТЧ = Новый Структура;
	Если ИмяФормы = "Документ.ПланДвиженияДС.Форма.ФормаДокумента" Тогда
		СписокРеквизитовТЧ.Вставить("СтатьяПлана",Новый Структура("ТипЗначения,Представление",Новый ОписаниеТипов("СправочникСсылка.СтатьиПлана"),"ru = 'Статья'; en = 'Item'"));
		СписокРеквизитовТЧ.Вставить("СуммаПриход",Новый Структура("ТипЗначения,Представление",мТипЧисло,"ru = 'Приход'; en = 'Income'"));
		СписокРеквизитовТЧ.Вставить("СуммаРасход",Новый Структура("ТипЗначения,Представление",мТипЧисло,"ru = 'Расход'; en = 'Expence'"));
	ИначеЕсли ИмяФормы = "Документ.ПриходДенег.Форма.ФормаДокумента" 
			Или ИмяФормы = "Документ.РасходДенег.Форма.ФормаДокумента" Тогда
		СписокРеквизитовТЧ.Вставить("СтатьяДДС",Новый Структура("ТипЗначения,Представление",Новый ОписаниеТипов("СправочникСсылка.СтатьиДДС"),"ru = 'Статья'; en = 'Item'"));
		СписокРеквизитовТЧ.Вставить("Сумма"    ,Новый Структура("ТипЗначения,Представление",мТипЧисло,"ru = 'Сумма'; en = 'Amount'"));
	КонецЕсли;
	Возврат СписокРеквизитовТЧ;	
КонецФункции