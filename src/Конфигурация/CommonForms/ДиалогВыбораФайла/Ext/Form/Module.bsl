&НаСервере
Процедура ОбновитьСписокФайловСервер (ИмяКаталога)
	ИмяКаталога = СокрЛП(ИмяКаталога);
	
	Если СтрЧислоВхождений(ИмяКаталога,Разделитель) > 1 И Прав(ИмяКаталога,1) = Разделитель Тогда
		ИмяКаталога = Лев(ИмяКаталога,СтрДлина(ИмяКаталога) - 1);
	КонецЕсли;
	
	мКаталог = Новый Файл(ИмяКаталога);
	
	Если ИмяКаталога <> Диск И Не мКаталог.Существует() Тогда
		
		мРодитель = Новый Файл(мКаталог.Путь);
		Если мРодитель.Существует() Тогда
			Путь = мРодитель.Путь;
		Иначе
			Путь = Диск;
		КонецЕсли;
		
		ОбновитьСписокФайловСервер(Путь);
		Возврат;
		
	ИначеЕсли мКаталог.ЭтоКаталог() Или ИмяКаталога = Диск Тогда
		Каталог  = ИмяКаталога;
		ТаблицаФайлов.Очистить();
		ТЗ       = ТаблицаФайлов.Выгрузить();
	
		мИмяРодителя = мКаталог.Путь;
		Если Не ПустаяСтрока(мИмяРодителя) Тогда
			НоваяСтрока  = ТаблицаФайлов.Добавить();
			Родитель     = Новый Файл(мИмяРодителя);
			Если Родитель.Существует() Тогда
				НоваяСтрока.Имя       = "..";
				НоваяСтрока.ПолноеИмя = Родитель.ПолноеИмя;
				НоваяСтрока.Путь      = Родитель.Путь;
				НоваяСтрока.Каталог   = Родитель.ЭтоКаталог();
				НоваяСтрока.Картинка  = БиблиотекаКартинок.УровеньВверх;
			КонецЕсли;
		КонецЕсли;
		
		Файлы = НайтиФайлы(ИмяКаталога,"*");
		Для Каждого Файл Из Файлы Цикл
			Если НЕ ПоказыватьСкрытыеФайлы И (Файл.ПолучитьНевидимость() Или Лев(Файл.Имя,1) = ".") Тогда
				Продолжить;
			КонецЕсли;	
			НоваяСтрока = ТЗ.Добавить();
			НоваяСтрока.Имя       = Файл.Имя;
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Путь      = Файл.Путь;
			НоваяСтрока.Каталог   = Файл.ЭтоКаталог();
		КонецЦикла;	
		
	ИначеЕсли мКаталог.ЭтоФайл Тогда
		
		// Ну мало ли..
		ОбновитьСписокФайловСервер(мКаталог.Путь);
		Возврат;
	КонецЕсли;	
	
	ТЗ.Сортировать("Каталог УБЫВ, Имя ВОЗР");
	
	Для Каждого СтрокаТЧ Из ТЗ Цикл
		НоваяСтрока = ТаблицаФайлов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЧ);
		Если НоваяСтрока.Каталог Тогда
			НоваяСтрока.Картинка = БиблиотекаКартинок.Папка;
		Иначе
			НоваяСтрока.Картинка = БиблиотекаКартинок.Документ;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

&НаКлиенте
Процедура ТаблицаФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекСтрока.Каталог Тогда
		мКаталог   = ТекСтрока.ПолноеИмя;
		ОбновитьСписокФайловСервер(мКаталог);
	Иначе
		ВыбранныйФайлИмя  = ТекСтрока.Имя;
		ВыбранныйФайлПуть = ТекСтрока.Путь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВыбранныйФайлИмя = Параметры.ИмяФайла;
	мСписок          = Элементы.Диск.СписокВыбора;
	
	Информация = Новый СистемнаяИнформация();
	Если Информация.ТипПлатформы = ТипПлатформы.Windows_x86 ИЛИ Информация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		Разделитель = "\";
		Выполнить("
		|fso = Новый COMОбъект (""Scripting.FileSystemObject"");
		|Для Каждого мДиск Из fso.Drives Цикл
		|	Если мДиск.IsReady Тогда
		|		мСписок.Добавить(мДиск.Path);
		|	КонецЕсли;
		|КонецЦикла;
		|");	
		Диск     = мСписок[0]; 
		мКаталог = мСписок[0]; 
	Иначе 
		Разделитель = "/";
		мКаталоги   = НайтиФайлы("/mnt","*");
		Для Каждого мКаталог Из мКаталоги Цикл
			Если Не мКаталог.Существует() Или Не мКаталог.ЭтоКаталог() Или мКаталог.ПолучитьНевидимость() Тогда
				Продолжить;
			КонецЕсли;
			мПолноеИмя = НРег(мКаталог.ПолноеИмя);
			Если Найти(мПолноеИмя,"sdcard") > 0 Или Найти(мПолноеИмя,"usbdrive") > 0 Тогда 
				мСписок.Добавить(мКаталог.ПолноеИмя);
			КонецЕсли;
		КонецЦикла;	
		мСписок.СортироватьПоЗначению(НаправлениеСортировки.Возр);
		Если мСписок.НайтиПоЗначению("/mnt/sdcard") = Неопределено Тогда
			Диск     = мСписок[0]; 
			мКаталог = мСписок[0]; 
		Иначе	
			Диск     = "/mnt/sdcard"; 
			мКаталог = "/mnt/sdcard";
		КонецЕсли;	
		//Элементы.Диск.СписокВыбора.Добавить("/mnt");
	КонецЕсли;	
	
	Если Элементы.Диск.СписокВыбора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокФайловСервер(мКаталог);
КонецПроцедуры

// Выбираем файл

&НаКлиенте
Процедура ВыбратьФайл(Команда)
	Если НЕ ЗначениеЗаполнено(ВыбранныйФайлИмя) Тогда
		Возврат;
	КонецЕсли;	
	Закрыть(Каталог + Разделитель + ВыбранныйФайлИмя);
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейти(Команда)
	ОбновитьСписокФайловСервер(Каталог);
КонецПроцедуры

&НаКлиенте
Процедура ДискПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Диск) Тогда
		ОбновитьСписокФайловСервер(Диск);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСкрытыеФайлыПриИзменении(Элемент)
	ОбновитьСписокФайловСервер(Каталог);
КонецПроцедуры
