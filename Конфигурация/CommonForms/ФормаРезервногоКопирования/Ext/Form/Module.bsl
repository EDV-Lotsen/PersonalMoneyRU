&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	ВыгрузитьДанныеСервер();
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДанныеСервер()
	ВыгрузитьДанныеВФайл();
КонецПроцедуры 	

// Выполняет выгрузку объектов //
&НаСервере
Функция ВыгрузитьДанныеВФайл() Экспорт
	ОбъектыМД = Новый Массив;
	ОбъектыМД.Добавить(Справочники);
	ОбъектыМД.Добавить(Документы);
	
	мЗаписьXML = Новый ЗаписьXML;
	мЗаписьXML.ОткрытьФайл(ПутьКФайлу, "UTF-8");
	
	мЗаписьXML.ЗаписатьОбъявлениеXML();

	мЗаписьXML.ЗаписатьНачалоЭлемента("Данные");    // ++ Данные
	
	мЗаписьXML.ЗаписатьНачалоЭлемента("Заголовок"); // ++ Заголовок
	мЗаписьXML.ЗаписатьАтрибут("Источник","ЛичныеДеньги");
	мЗаписьXML.ЗаписатьАтрибут("Версия"  ,Метаданные.Версия);
	мЗаписьXML.ЗаписатьКонецЭлемента();             // -- Заголовок
	
	Для Каждого Константа Из Константы Цикл 
		МенеджерЗначения = Константа.СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		ЗаписатьXML(мЗаписьXML,МенеджерЗначения);
	КонецЦикла;
	
	Для Каждого МД Из ОбъектыМД Цикл
		Для Каждого Эл Из МД Цикл 
			Выборка = Эл.Выбрать();
			Пока Выборка.Следующий() Цикл
				ВыгрузитьСсылку(мЗаписьXML,Выборка.Ссылка.ПолучитьОбъект());
			КонецЦикла;	
		КонецЦикла;	
	КонецЦикла;	
	
	мЗаписьXML.ЗаписатьКонецЭлемента();             // -- Данные
	мЗаписьXML.Закрыть();
	
	Сообщить(НСтр("ru = 'Данные успешно выгружены!'; en = 'Backup succesfully created!'"));
КонецФункции	

&НаСервере
Функция ВыгрузитьСсылку (ЗаписьXML, Элемент)
	ЗаписатьXML(ЗаписьXML,Элемент);
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Ссылка)) Тогда
		Для Каждого Набор Из Элемент.Движения Цикл
			Набор.Прочитать();
			Если Набор.Количество() > 0 Тогда
				ЗаписатьXML(ЗаписьXML,Набор);
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
КонецФункции	

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Рез = ОткрытьФормуМодально("ОбщаяФорма.ДиалогВыбораФайла",Новый Структура("НачальныйКаталог,ИмяФайла",ПутьКФайлу,"Data.xml"));
	Если Рез <> Неопределено Тогда
		ПутьКФайлу = Рез;
	КонецЕсли;	
КонецПроцедуры

// Выполняет загрузку объектов //
&НаКлиенте
Процедура ПрочитатьДанныеИзФайла(Команда)
	ПрочитатьДанныеИзФайлаСервер();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИзФайлаСервер ()
	Файл = Новый Файл(ПутьКФайлу);
	
	Если Не Файл.Существует() Тогда
		ВызватьИсключение("Файл не найден!");
	КонецЕсли;	
	
	Файл = Неопределено;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Ref","");
	СтруктураРеквизитов.Вставить("IsFolder","");
	СтруктураРеквизитов.Вставить("DeletionMark","");
	СтруктураРеквизитов.Вставить("Parent","");
	СтруктураРеквизитов.Вставить("Description","");
	СтруктураРеквизитов.Вставить("Code","");
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПутьКФайлу);
	ЧтениеXML.Прочитать(); // Область - Заголовок файла
	ЧтениеXML.Прочитать(); // Область - Данные
	ЧтениеXML.Прочитать(); // Область - Заголовок - Начало
	ЧтениеXML.Прочитать(); // Область - Заголовок - Конец
	Пока Истина Цикл
		Если ЧтениеXML.ЛокальноеИмя = "Данные" Тогда
			Прервать;
		КонецЕсли;	
		Попытка
			// Если все хорошо и структура данных не изменилась - считываем
			Данные = ПрочитатьXML(ЧтениеXML);
			Данные.ОбменДанными.Загрузка = Истина;
			Данные.Записать();
		Исключение
			ЛокИмя = ЧтениеXML.ЛокальноеИмя;
			ЧтениеXML.Прочитать();
		КонецПопытки;
	КонецЦикла;	
	
	Сообщить(НСтр("ru = 'Данные успешно восстановлены!'; en = 'Backup succesfully restored!'"));
КонецПроцедуры	
