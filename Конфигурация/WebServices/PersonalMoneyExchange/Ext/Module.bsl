
/// ОБРАБОТЧИКИ КОМАНД ВЕБ-СЕРВИСА ///

Функция НачатьОбмен(КодМобильногоКомпьютера, НаименованиеМобильногоКомпьютера, НомерОтправленного, НомерПринятого, Версия)
	СтруктураОтвета = Новый Структура("ОбменРазрешен, СообщениеОбОшибке, УдалитьДанныеНаУстройстве");
	
	СтруктураОтвета.ОбменРазрешен = Истина;
	СтруктураОтвета.СообщениеОбОшибке = "";
	СтруктураОтвета.УдалитьДанныеНаУстройстве = Ложь;
	
	мВерсияМП        = Лев(Версия,Найти(Версия,".") - 1);
	мВерсияПрограммы = Лев(Метаданные.Версия,Найти(Метаданные.Версия,".") - 1);
	
	// Проверяем соответствие версии МП //
	Если мВерсияМП < мВерсияПрограммы Тогда
		СтруктураОтвета.ОбменРазрешен = Ложь;
		СтруктураОтвета.СообщениеОбОшибке = НСтр("ru = 'Требуется обновление мобильного приложения!'; en = 'You need to upgrade your mobile app!'");
		Возврат Новый ХранилищеЗначения(СтруктураОтвета, Новый СжатиеДанных(9));
	ИначеЕсли мВерсияМП > мВерсияПрограммы Тогда
		СтруктураОтвета.ОбменРазрешен = Ложь;
		СтруктураОтвета.СообщениеОбОшибке = НСтр("ru = 'Требуется обновление конфигурации!'; en = 'You need to upgrade your local database!'");
		Возврат Новый ХранилищеЗначения(СтруктураОтвета, Новый СжатиеДанных(9));
	КонецЕсли;	
		
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверим правильно ли заполнен центральный узел //
	УзелОбмена = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.ЭтотУзел().ПолучитьОбъект();
	Если Не ЗначениеЗаполнено(УзелОбмена.Код) Тогда
		УзелОбмена.Код = "001";
		УзелОбмена.Наименование = НСтр("ru = 'Центральный'; en = 'Main database'");
		УзелОбмена.Записать();
	КонецЕсли;
	
	// Проверим есть ли узел для нашего МП //
	УзелОбмена = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.НайтиПоКоду(КодМобильногоКомпьютера);
	
	Если УзелОбмена.Пустая() Тогда
		
		НовыйУзел = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.СоздатьУзел();
		НовыйУзел.Код                = КодМобильногоКомпьютера;
		НовыйУзел.Наименование       = НаименованиеМобильногоКомпьютера;
		НовыйУзел.НомерОтправленного = НомерОтправленного;
		НовыйУзел.НомерПринятого     = НомерПринятого;
		НовыйУзел.Записать();
		//
		УзелОбмена = НовыйУзел.Ссылка;
	Иначе
		Если УзелОбмена.Наименование <> НаименованиеМобильногоКомпьютера Тогда
			Узел                 = УзелОбмена.ПолучитьОбъект();
			Узел.ПометкаУдаления = Ложь;
			Узел.Наименование    = НаименованиеМобильногоКомпьютера;
			Узел.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый ХранилищеЗначения(СтруктураОтвета, Новый СжатиеДанных(9));
	
КонецФункции

Функция ПолучитьДанные(КодМобильногоКомпьютера, ПараметрыОбмена)
	
	УзелОбмена = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.НайтиПоКоду(КодМобильногоКомпьютера);
	
	Если УзелОбмена.Пустая() Тогда
		ВызватьИсключение(НСтр("ru = 'Неизвестное устройство - '; en = 'Unknown device -'") + КодМобильногоКомпьютера);
	КонецЕсли;
	
	ПереданныеПараметрыОбмена = ПараметрыОбмена.Получить();
	
	ПереданныеПараметрыОбмена.Вставить("УзелОбмена", УзелОбмена);
	
	Возврат ОбменДаннымиМПЛичныеДеньги.ПолучитьДанныеОбмена(УзелОбмена);
КонецФункции

Функция ЗаписатьДанные(КодМобильногоКомпьютера, ДанныеМобильногоПриложения)
	УзелОбмена = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.НайтиПоКоду(КодМобильногоКомпьютера);
	
	Если УзелОбмена.Пустая() Тогда
		ВызватьИсключение(НСтр("ru='Неизвестное устройство - '") + КодМобильногоКомпьютера);
	КонецЕсли;
	
	ОбменДаннымиМПЛичныеДеньги.ПринятьПакетОбмена(УзелОбмена, ДанныеМобильногоПриложения);
КонецФункции

Функция ПолучитьСвободныйНомерУзла()
	// Проверим правильно ли заполнен центральный узел //
	УзелОбмена = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.ЭтотУзел().ПолучитьОбъект();
	Если Не ЗначениеЗаполнено(УзелОбмена.Код) Тогда
		УзелОбмена.Код = "001";
		УзелОбмена.Наименование = НСтр("ru = 'Центральный'; en = 'Main database'");
		УзелОбмена.Записать();
	КонецЕсли;
	
	Выборка   = ПланыОбмена.МобильноеПриложениеЛичныеДеньги.Выбрать();
	НомерУзла = 0;
	Пока Выборка.Следующий() Цикл
		Попытка
			НовыйНомер = Число(СокрЛП(Выборка.Код));
			НомерУзла  = Макс(НомерУзла,НовыйНомер);
		Исключение
		КонецПопытки;
	КонецЦикла;
	НомерУзла = НомерУзла + 1;
	Возврат Формат(НомерУзла,"ЧЦ=3; ЧН=000; ЧВН=; ЧГ=0");
КонецФункции
