
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ПланДвиженияДС 
	Движения.ПланДвиженияДС.Записывать = Истина;
	Движения.ПланДвиженияДС.Очистить();
	
	мДатаНач = ПолучитьНачалоПериода(НачалоПериода,Периодичность);
	мДатаКон = ПолучитьКонецПериода(КонецПериода,Периодичность);
	
	мДата = мДатаНач;
	Пока мДата < мДатаКон Цикл
		Для Каждого ТекСтрокаПлан Из План Цикл
			Движение = Движения.ПланДвиженияДС.Добавить();
			Движение.Период        = мДата;
			Движение.СтатьяПлана   = ТекСтрокаПлан.СтатьяПлана;
			Движение.Периодичность = Периодичность;
			Движение.СуммаПриход   = ТекСтрокаПлан.СуммаПриход;
			Движение.СуммаРасход   = ТекСтрокаПлан.СуммаРасход;
		КонецЦикла;
		мДата = ПолучитьКонецПериода(мДата,Периодичность) + 1;
	КонецЦикла; 
КонецПроцедуры

Функция ПолучитьНачалоПериода(Дата,Периодичность)
	Если Периодичность = Перечисления.Периодичность.Квартал Тогда
		Возврат НачалоКвартала(Дата);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		Возврат НачалоМесяца(Дата);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		Возврат НачалоНедели(Дата);
	Иначе
		Возврат НачалоДня(Дата);
	КонецЕсли;	
КонецФункции	

Функция ПолучитьКонецПериода(Дата,Периодичность)
	Если Периодичность = Перечисления.Периодичность.Квартал Тогда
		Возврат КонецКвартала(Дата);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		Возврат КонецМесяца(Дата);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		Возврат КонецНедели(Дата);
	Иначе
		Возврат КонецДня(Дата);
	КонецЕсли;	
КонецФункции	

